plugins {
    id 'org.eclipse.jkube.kubernetes' version '1.10.1'
    id 'org.springframework.boot' version '3.0.1'
    id 'ovh.equino.version' version '0.1.1'
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'

    repositories {
        mavenCentral()
        maven { url 'https://nexus.cloud.equino.ovh/repository/m2' }
    }

    publishing {
        repositories {
            maven {
                name = "EquinoNexus"
                url = "https://nexus.cloud.equino.ovh/repository/m2"
                credentials {
                    username = project.findProperty("equinoNexus.username")
                    password = project.findProperty("equinoNexus.password")
                }
            }
        }
    }

    group = 'ovh.equino.actracker-api'

    task setupInfra(type: Task)
    task tearDownInfra(type: Task)
}

subprojects {

    sourceSets {
        integrationTest {
            java {
                compileClasspath += main.output + test.output
                runtimeClasspath += main.output + test.output
                srcDir file("src/integration-test/java")
            }
            resources.srcDir file("src/integration-test/resources")
        }
    }

    configurations {
        integrationTestImplementation.extendsFrom testImplementation
        integrationTestRuntime.extendsFrom testRuntime
    }

    dependencies {
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.3'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.3'
    }

    test {
        useJUnitPlatform()
    }

    task integrationTest(type: Test) {
        useJUnitPlatform()
        testClassesDirs = sourceSets.integrationTest.output.classesDirs
        classpath = sourceSets.integrationTest.runtimeClasspath
        outputs.upToDateWhen { false }
    }

    integrationTest.mustRunAfter test
}

dependencies {
    runtimeOnly project(':actracker-api-main-springboot')
}

springBoot {
    mainClass = 'ovh.equino.actracker.main.springboot.ActrackerApi'
}
