import static java.util.Optional.ofNullable

plugins {
    id 'org.eclipse.jkube.kubernetes' version '1.15.0'
    id 'org.springframework.boot' version '3.1.0'
    id 'ovh.equino.version' version '0.2.2'
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'

    sourceCompatibility = 17
    targetCompatibility = 17

    repositories {
        mavenCentral()
        maven {
            name 'mavenEquino'
            url 'https://maven.cloud.equino.ovh/equino'
            credentials {
                username project.findProperty('mavenEquinoUsername')
                password project.findProperty('mavenEquinoPassword')
            }
        }
        maven {
            name 'mavenEquinoSnapshot'
            url 'https://maven.cloud.equino.ovh/equinoSnapshot'
            credentials {
                username project.findProperty('mavenEquinoUsername')
                password project.findProperty('mavenEquinoPassword')
            }
        }
    }

    publishing {
        repositories {
            maven {
                name = "mavenEquino"
                url = ofNullable(project.findProperty("mavenEquinoUrl"))
                        .orElse("https://maven.cloud.equino.ovh/equinoSnapshot")
                credentials {
                    username = project.findProperty("mavenEquinoUsername")
                    password = project.findProperty("mavenEquinoPassword")
                }
            }
        }
    }

    group = 'ovh.equino.actracker-api'

    task setupInfra(type: Task)
    task tearDownInfra(type: Task)
}

subprojects {

    sourceSets {
        integrationTest {
            java {
                compileClasspath += main.output + test.output
                runtimeClasspath += main.output + test.output
                srcDir file("src/integration-test/java")
            }
            resources.srcDir file("src/integration-test/resources")
        }
    }

    configurations {
        integrationTestImplementation.extendsFrom testImplementation
        integrationTestRuntime.extendsFrom testRuntime
        integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
    }

    dependencies {
        // TODO should be also provided for integration test?
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.1'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.1'
        integrationTestImplementation 'org.assertj:assertj-core:3.24.2'
        integrationTestImplementation 'org.junit.jupiter:junit-jupiter-params:5.10.1'
    }

    test {
        useJUnitPlatform()
    }

    task integrationTest(type: Test) {
        useJUnitPlatform()
        testClassesDirs = sourceSets.integrationTest.output.classesDirs
        classpath = sourceSets.integrationTest.runtimeClasspath
        outputs.upToDateWhen { false }
    }

    integrationTest.mustRunAfter test
}

dependencies {
    runtimeOnly project(':actracker-api-main-springboot')
}

springBoot {
    mainClass = 'ovh.equino.actracker.main.springboot.ActrackerApi'
}
